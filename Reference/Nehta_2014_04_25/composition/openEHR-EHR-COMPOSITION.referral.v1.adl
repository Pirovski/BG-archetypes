archetype (adl_version=1.4)
	openEHR-EHR-COMPOSITION.referral.v1

concept
	[at0000]	-- Referral Document
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"NHS CfH/Ocean Informatics">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2008-01-08">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To communicate all information required to make a request for provision of a specified service by another healthcare provider or organisation as a single document.">
			use = <"Use to communicate all information required to make a request for provision of a specified service by another healthcare provider or organisation as a single document.

Note that 'DateTime Attested', 'Author', 'Patient' details, 'Facility', 'Specific Location', 'Health Event DateTime started' and 'Health Event DateTime ended' are not modelled explicitly in this archetype but  found in the reference model.">
			keywords = <"referral", "request">
			misuse = <"">
			copyright = <"© openEHR">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia", ...>
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["MD5-CAM-1.0.1"] = <"6E29FBEAF5BAA46D66D2C41B4750ADA7">
	>

definition
	COMPOSITION[at0000] matches {	-- Referral Document
		category matches {
			DV_CODED_TEXT matches {
				defining_code matches {[openehr::433]}
			}
		}
		context matches {
			EVENT_CONTEXT matches {
				participations cardinality matches {0..*; unordered} matches {
					PARTICIPATION occurrences matches {0..1} matches {
						function matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0033, 	-- Referrer
									at0034, 	-- Receiver
									at0035, 	-- CC Clinician/Organisation
									at0037]	-- General Practitioner
								}
							}
						}
					}
				}
				other_context matches {
					ITEM_TREE[at0001] matches {	-- Tree
						items cardinality matches {3..*; unordered} matches {
							ELEMENT[at0028] occurrences matches {0..1} matches {	-- Requestor Identifier
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0038] occurrences matches {0..*} matches {	-- Patient
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0040] occurrences matches {0..1} matches {	-- Document Status
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Referral Document">
					description = <"A document expressing all information required to make a request for provision of a specified service by another healthcare provider or organisation.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0028"] = <
					text = <"Requestor Identifier">
					description = <"Copy of INSTRUCTION.request-referral's Requestor Identifier.">
				>
				["at0033"] = <
					text = <"Referrer">
					description = <"Clinician or organisation making the request for service.">
				>
				["at0034"] = <
					text = <"Receiver">
					description = <"Clinician or organisation receiving the request for service.">
				>
				["at0035"] = <
					text = <"CC Clinician/Organisation">
					description = <"Other clinicians or organisations who need to receive a copy of the referral.">
				>
				["at0037"] = <
					text = <"General Practitioner">
					description = <"Subject's usual primary care clinician.">
				>
				["at0038"] = <
					text = <"Patient">
					description = <"Details about the patient to be explicitly expressed in the referral document.">
				>
				["at0040"] = <
					text = <"Document Status">
					description = <"Status of the referral.">
				>
			>
		>
	>
