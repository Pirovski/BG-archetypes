archetype (adl_version=2.0.5; rm_release=0.9.0)
	CDISC-Bridg-PerformedObservation.vital_sign.v1.0.0

specialize
	CDISC-Bridg-PerformedObservation.observation.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"10/06/2014">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Example CDISC archetype representing vital sign observation">
			keywords = <"Observation", ...>
		>
	>
	lifecycle_state = <"unmanaged">
	copyright = <"Â© 2014 CDISC.org">

definition
	PerformedObservation[id1.1] matches {	-- vital sign BRIDG observation
		instantiatedDefinedActivity matches {
			DefinedObservation[id0.2] matches {
				nameCode matches {[ac0.1]}		-- vital sign measurement type
			}
		}
		instantiatedPlannedActivity matches {
			PlannedActivity[id0.8] matches {
				componentPlannedCompositionRelationship matches {
					PlannedCompositionRelationship[id0.9] matches {	-- Encounter type
						componentPlannedActivity matches {
							PlannedSubjectActivityGroup[id0.10] occurrences matches {1} 
						}
					}
				}
			}
		}
		resultedPerformedObservationResult matches {
			PerformedObservationResult[id0.4] 	-- Original value
			PerformedObservationResult[id0.5] matches {	-- Original units
				value matches {
					ST[id0.12] 
				}
			}
			PerformedObservationResult[id0.6] matches {	-- Result value
				baselineIndicator existence matches {1} 
			}
		}
		bodyPositionCode matches {[ac0.2]}		-- vital sign measurement body positions
		approachAnatomicSiteCode matches {[ac0.3]}		-- vital sign measurement body sites
		dateRange existence matches {1} 
	}

terminology
	term_definitions = <
		["en"] = <
			["ac0.1"] = <
				text = <"vital sign measurement type">
				description = <"vital sign measurement type">
			>
			["ac0.2"] = <
				text = <"vital sign measurement body positions">
				description = <"vital sign measurement body positions">
			>
			["ac0.3"] = <
				text = <"vital sign measurement body sites">
				description = <"vital sign measurement body sites">
			>
			["at0.10"] = <
				text = <"Chest">
				description = <"Chest">
			>
			["at0.100"] = <
				text = <"SYSBP">
				description = <"systolic blood pressure">
			>
			["at0.101"] = <
				text = <"DIABP">
				description = <"diastolic blood pressure">
			>
			["at0.102"] = <
				text = <"BMI">
				description = <"Body mass index">
			>
			["at0.103"] = <
				text = <"HR">
				description = <"Heart rate">
			>
			["at0.104"] = <
				text = <"HEIGHT">
				description = <"Height">
			>
			["at0.105"] = <
				text = <"WEIGHT">
				description = <"Weight">
			>
			["at0.106"] = <
				text = <"TEMP">
				description = <"Body temperature">
			>
			["at0.107"] = <
				text = <"RESP">
				description = <"Respiration">
			>
			["at0.11"] = <
				text = <"Abdomen">
				description = <"Abdomen">
			>
			["at0.2"] = <
				text = <"Left arm">
				description = <"Left arm">
			>
			["at0.200"] = <
				text = <"SITTING">
				description = <"Sitting position">
			>
			["at0.201"] = <
				text = <"STANDING">
				description = <"Standing position">
			>
			["at0.202"] = <
				text = <"SUPINE">
				description = <"Supine position">
			>
			["at0.203"] = <
				text = <"INCLINED">
				description = <"Inclined position">
			>
			["at0.3"] = <
				text = <"Right arm">
				description = <"Right arm">
			>
			["at0.4"] = <
				text = <"Left thigh">
				description = <"Left thigh">
			>
			["at0.5"] = <
				text = <"Right thigh">
				description = <"Right thigh">
			>
			["at0.6"] = <
				text = <"Head">
				description = <"Head">
			>
			["at0.7"] = <
				text = <"Neck">
				description = <"Neck">
			>
			["at0.8"] = <
				text = <"Left wrist">
				description = <"Left wrist">
			>
			["at0.9"] = <
				text = <"Right wrist">
				description = <"Right wrist">
			>
			["id0.4"] = <
				text = <"Original value">
				description = <"Original measurement value collected or received">
			>
			["id0.5"] = <
				text = <"Original units">
				description = <"Original measurement units collected or received">
			>
			["id0.6"] = <
				text = <"Result value">
				description = <"Result value for all findings">
			>
			["id0.9"] = <
				text = <"Encounter type">
				description = <"Encounter type">
			>
			["id1.1"] = <
				text = <"vital sign BRIDG observation">
				description = <"vital sign BRIDG observation">
			>
		>
	>
	value_sets = <
		["ac0.1"] = <
			id = <"ac0.1">
			members = <"at0.100", "at0.101", "at0.102", "at0.103", "at0.104", "at0.105", "at0.106", "at0.107">
		>
		["ac0.2"] = <
			id = <"ac0.2">
			members = <"at0.200", "at0.201", "at0.202", "at0.203">
		>
		["ac0.3"] = <
			id = <"ac0.3">
			members = <"at0.2", "at0.3", "at0.4", "at0.5", "at0.6", "at0.7", "at0.8", "at0.9", "at0.10", "at0.11">
		>
	>

annotations
	items = <
		["en"] = <
			items = <
				["/involvedSubject[id2]"] = <
					items = <
						["xls-ref"] = <"635">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"USUBJID">
					>
				>
				["/instantiatedDefinedActivity[id0.2]"] = <
					items = <
						["xls-ref"] = <"639">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSTESTCD">
					>
				>
				["/resultedPerformedObservationResult[id0.4]"] = <
					items = <
						["xls-ref"] = <"644">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSORRES">
					>
				>
				["/resultedPerformedObservationResult[id0.5]"] = <
					items = <
						["xls-ref"] = <"645">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSORRESU">
					>
				>
				["/resultedPerformedObservationResult[id0.6]"] = <
					items = <
						["xls-ref"] = <"646">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSTRESC">
					>
				>
				["/bodyPositionCode"] = <
					items = <
						["xls-ref"] = <"643">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSPOS">
					>
				>
				["/approachAnatomicSiteCode"] = <
					items = <
						["xls-ref"] = <"651">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSLOC">
					>
				>
				["/dateRange"] = <
					items = <
						["xls-ref"] = <"664">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSRFTDTC">
					>
				>
				["/instantiatedPlannedActivity[id0.8]/componentPlannedCompositionRelationship[id0.9]/componentPlannedActivity[id0.10]"] = <
					items = <
						["xls-ref"] = <"656">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VISIT">
					>
				>
				["/resultedPerformedObservationResult[id0.6]/baselineIndicator"] = <
					items = <
						["xls-ref"] = <"653">
						["SDTM-domain"] = <"VS">
						["SDTM-Variable"] = <"VSBLFL">
					>
				>
			>
		>
	>
