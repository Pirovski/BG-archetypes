archetype (adl_version=1.5)
	openEHR-EHR-OBSERVATION.aml_thyroid_test.v1

specialize
	openEHR-EHR-OBSERVATION.aml_lab_test.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics, Australia">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"26/03/2006">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the function of the thyroid gland and its pituitary stimulation.">
			use = <"">
			keywords = <"lab", "pathology", "biochemistry", "laboratory", "TSH", "Thyroid function", "TSH">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_details = <
		["MD5-CAM-1.0.1"] = <"C1A6FF145D31C5A8488AB9F77DAB4609">
		["references"] = <"NEHTA, Australia. Pathology Episode v1.0 - Data Group library [Internet]. 2009;[cited 2009 Dec 8 ] Available from: http://www.nehta.gov.au/data-group-library/data-specifications/dgl-pathology

IHE. IHE, Laboratory Technical Framework, Volume 3 Content [Internet]. 2008;Available from: http://www.ihe.net/Technical_Framework/upload/ihe_lab_TF_rel2_1-Vol-3_FT_2008-08-08.pdf




">
	>

definition
	OBSERVATION[at0000.1] matches {	-- Thyroid function tests
		/data[at0001]/events matches {
			EVENT[at0002.1] occurrences matches {0..1} matches {	-- Any event
				data matches {
					ITEM_TREE[at0003] matches {
						items matches {
							ELEMENT[at0078.2] occurrences matches {0..1} matches {	-- Thyroid stimulating hormone (TSH)
								value matches {
									(C_DV_QUANTITY) <
										property = <[openehr::119]>
										list = <
											["1"] = <
												units = <"mIU/l">
												magnitude = <|0.0..100.0|>
											>
										>
									>
								}
							}
							ELEMENT[at0078.7] occurrences matches {0..1} matches {	-- Free Triiodothyronine (Free T3)
								value matches {
									(C_DV_QUANTITY) <
										property = <[openehr::119]>
										list = <
											["1"] = <
												units = <"pmol/l">
												magnitude = <|0.0..1000.0|>
											>
										>
									>
								}
							}
							ELEMENT[at0078.8] occurrences matches {0..1} matches {	-- Total Triiodothyronine (Total T3)
								value matches {
									(C_DV_QUANTITY) <
										property = <[openehr::119]>
										list = <
											["1"] = <
												units = <"pmol/l">
												magnitude = <|0.0..1000.0|>
											>
										>
									>
								}
							}
							ELEMENT[at0078.3] occurrences matches {0..1} matches {	-- Free thyroxine (Free T4)
								value matches {
									(C_DV_QUANTITY) <
										property = <[openehr::119]>
										list = <
											["1"] = <
												units = <"pmol/l">
												magnitude = <|0.0..1000.0|>
											>
										>
									>
								}
							}
							ELEMENT[at0078.4] occurrences matches {0..1} matches {	-- Total Thyroxine (Total T4)
								value matches {
									(C_DV_QUANTITY) <
										property = <[openehr::119]>
										list = <
											["1"] = <
												units = <"pmol/l">
												magnitude = <|0.0..1000.0|>
											>
										>
									>
								}
							}
							ELEMENT[at0078.5] occurrences matches {0..1} matches {	-- T4 loaded uptake
								value matches {
									DV_QUANTITY matches {*}
								}
							}
							ELEMENT[at0078.9] occurrences matches {0..1} matches {	-- Free Triiodothyronine index (Free T3 index)
								value matches {
									DV_PROPORTION matches {
										numerator matches {|>=0.0|}
										denominator matches {|1.0|}
										type matches {1}
									}
								}
							}
							ELEMENT[at0078.6] occurrences matches {0..1} matches {	-- Free thyroxine index (FTI)
								value matches {
									DV_PROPORTION matches {
										numerator matches {|>=0.0|}
										denominator matches {|1.0|}
										type matches {1}
									}
								}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			["at0000.1"] = <
				text = <"Thyroid function tests">
				description = <"Measurement of biochemical markers of thyroid function.">
			>
			["at0002.1"] = <
				text = <"Any event">
				description = <"*">
			>
			["at0078.2"] = <
				text = <"Thyroid stimulating hormone (TSH)">
				description = <"TSH is released by the pituitary gland to stimulate release of thyroxine. Estimation by a high sensitivity assay is now accepted as the first line test for assessment of thyroid function.">
			>
			["at0078.3"] = <
				text = <"Free thyroxine (Free T4)">
				description = <"Concentration of T4 unbound">
			>
			["at0078.4"] = <
				text = <"Total Thyroxine (Total T4)">
				description = <"Concentration of T4 bound and unbound. ">
			>
			["at0078.5"] = <
				text = <"T4 loaded uptake">
				description = <"Uptake of T3 after loading the serum with T4.">
			>
			["at0078.6"] = <
				text = <"Free thyroxine index (FTI)">
				description = <"Total T4 x T3 uptake">
			>
			["at0078.7"] = <
				text = <"Free Triiodothyronine (Free T3)">
				description = <"Concentration of T3 not bound to proteins">
			>
			["at0078.8"] = <
				text = <"Total Triiodothyronine (Total T3)">
				description = <"Concentration of T3 bound and unbound">
			>
			["at0078.9"] = <
				text = <"Free Triiodothyronine index (Free T3 index)">
				description = <"Total T3 concentration x T3 uptake">
			>
		>
	>
