archetype (adl_version=1.5)
	CIMI-Core-ENTRY.aml_problem.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"23/04/2006">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"AML test archetype.">
			use = <"AML test use">
			keywords = <"issue", "condition", "problem", "obstacle">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_details = <
		["MD5-CAM-1.0.1"] = <"4A7BAF9ECB8E210E032DAC7C90FBF7D2">
		["references"] = <"">
	>

definition
	ENTRY[at0000] matches {	-- Problem
		data matches {
			CLUSTER[at0001] matches {	-- structure
				item cardinality matches {1..*; ordered} matches {
					ELEMENT[at0002] matches {	-- Problem
						value matches {
							TEXT matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Clinical description
						value matches {
							TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Date of initial onset
						value matches {
							DATE matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Age at initial onset
						value matches {
							DURATION matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Severity
						value matches {
								[local::
								at0006, 	-- Mild
								at0007, 	-- Moderate
								at0008]	-- Severe
						}
					}
					ELEMENT[at0038] occurrences matches {0..1} matches {	-- Clinical significance
						value matches {
							TEXT matches {*}
						}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Date clinically recognised
						value matches {
							DATE matches {
								value matches {yyyy-??-XX}
							}
						}
					}
					ELEMENT[at0037] occurrences matches {0..1} matches {	-- Age when clinically recognised
						value matches {
							DURATION matches {*}
						}
					}
					CLUSTER[at0011] occurrences matches {0..*} matches {	-- Location
						item cardinality matches {1..2; ordered} matches {
							ELEMENT[at0012] occurrences matches {0..1} matches {	-- Body site
								value matches {[ac0001]}		-- Any term that describes a body site
							}
							ELEMENT[at0013] occurrences matches {0..1} matches {	-- Location description
								value matches {
									TEXT matches {*}
								}
							}
						}
					}
					CLUSTER[at0014] occurrences matches {0..*} matches {	-- Aetiology
						item cardinality matches {1..*; unordered} matches {
							ELEMENT[at0015] occurrences matches {0..*} matches {	-- Agent
								value matches {
									TEXT matches {*}
								}
							}
							ELEMENT[at0016] occurrences matches {0..1} matches {	-- Complication of
								value matches {
									TEXT matches {*}
									URI matches {*}
								}
							}
							ELEMENT[at0017] occurrences matches {0..1} matches {	-- Description
								value matches {
									TEXT matches {*}
								}
							}
						}
					}
					CLUSTER[at0018] occurrences matches {0..1} matches {	-- Occurrences or exacerbations
						item cardinality matches {1..*; ordered} matches {
							ELEMENT[at0019] occurrences matches {0..1} matches {	-- Frequency of reccurrence
								value matches {
									QUANTITY matches {*}
								}
							}
							ELEMENT[at0020] occurrences matches {0..1} matches {	-- Date of last occurrence
								value matches {
									DATE matches {*}
								}
							}
							CLUSTER[at0021] occurrences matches {0..*} matches {	-- Occurrence/exacerbation
								item cardinality matches {1..*; unordered} matches {
									ELEMENT[at0022] occurrences matches {0..1} matches {	-- Clinical description
										value matches {
											TEXT matches {*}
										}
									}
									ELEMENT[at0023] occurrences matches {0..1} matches {	-- Outcome
										value matches {
											TEXT matches {*}
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Date of onset of occurrence
										value matches {
											DATE matches {*}
										}
									}
									ELEMENT[at0036] occurrences matches {0..1} matches {	-- Date of resolution of occurrence
										value matches {
											DATE matches {*}
										}
									}
								}
							}
							ELEMENT[at0025] occurrences matches {0..1} matches {	-- Number of occurrences
								value matches {
									COUNT matches {
										value matches {|>=1|}
									}
								}
							}
						}
					}
					CLUSTER[at0026] occurrences matches {0..1} matches {	-- Related problems
						item cardinality matches {1..*; unordered} matches {
							CLUSTER[at0027] occurrences matches {0..*} matches {	-- Related problem
								item cardinality matches {1..*; unordered} matches {
									ELEMENT[at0028] occurrences matches {0..1} matches {	-- Related problem
										value matches {
											TEXT matches {*}
											URI matches {*}
										}
									}
									ELEMENT[at0029] occurrences matches {0..1} matches {	-- Clinical description
										value matches {
											TEXT matches {*}
										}
									}
								}
							}
						}
					}
					ELEMENT[at0030] occurrences matches {0..1} matches {	-- Date of resolution
						value matches {
							DATE matches {*}
						}
					}
					ELEMENT[at0031] occurrences matches {0..1} matches {	-- Age at resolution
						value matches {
							DURATION matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			["at0000"] = <
				text = <"Problem">
				description = <"An issue or obstacle which adversely impacts on the physical, mental and/or social well-being of an individual. The definition of a problem is deliberately kept rather loose so as to capture any real or perceived concerns that may adversely affect an individual's wellbeing to any degree.  ">
			>
			["at0001"] = <
				text = <"structure">
				description = <"@ internal @">
			>
			["at0002"] = <
				text = <"Problem">
				description = <"Identification of the index problem.">
			>
			["at0003"] = <
				text = <"Date of initial onset">
				description = <"The date when the problem was first identified by the individual.">
			>
			["at0004"] = <
				text = <"Age at initial onset">
				description = <"The age of the individual when the problem was first identified by the individual.">
			>
			["at0005"] = <
				text = <"Severity">
				description = <"The assessed overall severity of the index problem.">
			>
			["at0006"] = <
				text = <"Mild">
				description = <"The severity of the index problem is mild, not affecting life or longevity.">
			>
			["at0007"] = <
				text = <"Moderate">
				description = <"The severity of the index problem is such as to cause significant morbidity.">
			>
			["at0008"] = <
				text = <"Severe">
				description = <"The severity of the index problem has had a major effect on life and/or longevity.">
			>
			["at0009"] = <
				text = <"Clinical description">
				description = <"Description of the clinical aspects of the problem.">
			>
			["at0010"] = <
				text = <"Date clinically recognised">
				description = <"Date the problem was recognised by a healthcare provider.">
			>
			["at0011"] = <
				text = <"Location">
				description = <"Location of the problem in terms of body site.">
			>
			["at0012"] = <
				text = <"Body site">
				description = <"The body site affected.">
			>
			["at0013"] = <
				text = <"Location description">
				description = <"Description of the location.">
			>
			["at0014"] = <
				text = <"Aetiology">
				description = <"Agents or Factors known to have been of aetiological significance.">
			>
			["at0015"] = <
				text = <"Agent">
				description = <"Agent or other factor known to have caused this problem.">
			>
			["at0016"] = <
				text = <"Complication of">
				description = <"Identification of a problem, injury or condition as text,coded text or link within the EHR.">
			>
			["at0017"] = <
				text = <"Description">
				description = <"Description of aetiological process.">
			>
			["at0018"] = <
				text = <"Occurrences or exacerbations">
				description = <"Grouping of information about individual occurrences or exacerbations.">
			>
			["at0019"] = <
				text = <"Frequency of reccurrence">
				description = <"The frequency of individual occurrences of the problem.">
			>
			["at0020"] = <
				text = <"Date of last occurrence">
				description = <"The date of the last occurrence of the problem.">
			>
			["at0021"] = <
				text = <"Occurrence/exacerbation">
				description = <"Information about one occurrence or exacerbation.">
			>
			["at0022"] = <
				text = <"Clinical description">
				description = <"A description of the exacerbation or occurrence.">
			>
			["at0023"] = <
				text = <"Outcome">
				description = <"Outcome of the occurrence or exacerbation.">
			>
			["at0024"] = <
				text = <"Date of onset of occurrence">
				description = <"Date of onset of occurrence or exacerbation.">
			>
			["at0025"] = <
				text = <"Number of occurrences">
				description = <"Number of times this problem has occurred or been apparent.">
			>
			["at0026"] = <
				text = <"Related problems">
				description = <"Further problems that are related in some way to the index problem.">
			>
			["at0027"] = <
				text = <"Related problem">
				description = <"A group of characteristics of the problem complicating the index problem.">
			>
			["at0028"] = <
				text = <"Related problem">
				description = <"Identification of a related problem as text,coded text or link within the EHR.">
			>
			["at0029"] = <
				text = <"Clinical description">
				description = <"Decription of the related problem.">
			>
			["at0030"] = <
				text = <"Date of resolution">
				description = <"The date that the problem resolved or went into remission.">
			>
			["at0031"] = <
				text = <"Age at resolution">
				description = <"The age of the person at the resolution of the problem.">
			>
			["at0032"] = <
				text = <"Tree">
				description = <"@ internal @">
			>
			["at0033"] = <
				text = <"References">
				description = <"Useful information about this condition.">
			>
			["at0034"] = <
				text = <"Reference">
				description = <"Reference to material about this condition.">
			>
			["at0035"] = <
				text = <"Web link">
				description = <"Useful information on the internet about this condition.">
			>
			["at0036"] = <
				text = <"Date of resolution of occurrence">
				description = <"Date of the resolution of the occurrence or exacerbation.">
			>
			["at0037"] = <
				text = <"Age when clinically recognised">
				description = <"Age at which the problem was recognised by a healthcare provider.">
			>
			["at0038"] = <
				text = <"Clinical significance">
				description = <"Description of the clinical significance of the problem in terms of impact of the problem on the patient.">
			>
		>
	>
	constraint_definitions = <
		["pt-br"] = <
			["ac0001"] = <
				text = <"Qualquer termo que descreve a localização do corpo.">
				description = <"Uma estrutura anatômica qualificada.">
			>
		>
		["en"] = <
			["ac0001"] = <
				text = <"Any term that describes a body site">
				description = <"An anatomical structure with qualifiers">
			>
		>
	>
