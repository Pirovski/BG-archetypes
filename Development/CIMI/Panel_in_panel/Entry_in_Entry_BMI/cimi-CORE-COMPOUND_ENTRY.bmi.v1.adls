archetype (adl_version=1.5.1)
	CIMI-Core-COMPOUND_ENTRY.bmi.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"CIMI">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"10/02/2014">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"CIMI Compund Entry that directly includes other 'atomic' Entries">
			copyright = <"Â© 2014 CIMI">
		>
	>
	lifecycle_state = <"unmanaged">

definition
	COMPOUND_ENTRY[id1] matches {	-- Multi-level panel
		parts matches {
			use_archetype INDIVISIBLE_ENTRY[CIMI-Core-INDIVISIBLE_ENTRY.body_weight.v1.0.108]  occurrences matches {1}
			use_archetype INDIVISIBLE_ENTRY[CIMI-Core-INDIVISIBLE_ENTRY.body_height.v1.0.109]  occurrences matches {1}
                }
		data matches {
			ELEMENT[id3] occurrences matches {1} matches {
				value matches {
					QUANTITY[id4] matches {
						value matches {|5.0..60.0|}
						units matches {[at1]}
					}
				}
			}
		}
	}

rules
	/data[id3]/value/value = /parts[CIMI-Core-INDIVISIBLE_ENTRY.body_weight.v1.0.108]/data[id2]/value/value  / /parts[CIMI-Core-INDIVISIBLE_ENTRY.body_height.v1.0.109]/data[id2]/value/value ^ 2

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"BMI panel">
				description = <"BMI panel">
			>
			["id3"] = <
				text = <"BMI measurement">
				description = <"BMI measurement">
			>
			["at1"] = <
				text = <"kg/m^2">
				description = <"kg/m^2">
			>
		>
	>
        term_bindings = <
                ["snomed-ct"] = <
                         ["at1"] = <http://snomedct.info/id/123456789>
                >
        >
