archetype (adl_version=1.5)
	CIMI-Core-ENTRY.openehr_body_temperature.v1

specialize
	CIMI-Core-ENTRY.openehr_observation.v1

language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"18/05/2004">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the measured temperature of a person - as a surrogate for the temperature of the whole body.">
			use = <"Used for recording the whole body temperature of a person or body.
Additional clusters can be included to provide additional state data - including environmental conditions, menstrual cycle details and exertion details, where appropriate.
Please Note: The site and method of recording may need to be displayed to the end user to facilitate accurate interpretation of the temperature recorded.">
			keywords = <"temperature", "body", "core", "fever", "hypothermia", "hyperthermia">
			misuse = <"This archetype is not to be used to record the temperature of any other object.
This archetype is not to be used to record the temperature of a part of the body in isolation e.g. temperature of the sole of the foot as a part of chronic diabetes management.">
			copyright = <"© 2011 openEHR Foundation">
		>
	>
	lifecycle_state = <"Draft">
	other_contributors = <"Knut Bernstein", "Sebastian Garde", "Ian McNicoll", "Omer Hotomaroglu", "Sundaresan Jaganathan", "Heather Leslie", "Shahla Foozonkhah">

definition
	ENTRY[at0000.1.1] matches {	-- Body temperature
		/data[at0.2]/items[at0.7]/items[at0.8]/items matches {
			CLUSTER[at0.10.1] matches {	-- data
				items cardinality matches {1; ordered} matches {
					ELEMENT[at0.0.1] matches {	-- Temperature
						value matches {
							(C_DV_QUANTITY) <
								property = <[openehr::127]>
								list = <
									["1"] = <
										units = <"°C">
										precision = <|1|>
									>
									["2"] = <
										units = <"°F">
										precision = <|1|>
									>
								>
							>
						}
					}
				}
			}
			CLUSTER[at0.11.1] matches {	-- state
				items matches {
					ELEMENT[at0.0.2] occurrences matches {0..1} matches {	-- Body exposure
						value matches {
							CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.0.31, 	-- Naked
									at0.0.32, 	-- Reduced clothing/bedding
									at0.0.33, 	-- Appropriate clothing/bedding
									at0.0.34; 	-- Increased clothing/bedding
									at0.0.33]	-- assumed value
								}
							}
						}
					}
				}
			}
		}
		/data matches {
			ELEMENT[at0001.0.1] occurrences matches {0..1} matches {	-- Protocol - site of measurement
				value matches {
					CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.0.22, 	-- Mouth
							at0.0.23, 	-- Ear canal
							at0.0.24, 	-- Axilla
							at0.0.25, 	-- Rectum
							at0.0.26, 	-- Nasopharynx
							at0.0.27, 	-- Urinary bladder
							at0.0.28, 	-- Intravascular
							at0.0.43, 	-- Skin
							at0.0.51, 	-- Vagina
							at0.0.54, 	-- Oesophagus
							at0.0.55]	-- Inguinal skin crease
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			["at0000.1.1"] = <
				text = <"Body temperature">
				description = <"A measurement of the body temperature, which is a surrogate for the whole body temperature of the person.">
			>
			["at0001.0.1"] = <
				text = <"Protocol - site of measurement">
				description = <"xxxxxxxxxxx">
			>
			["at0.0.1"] = <
				text = <"Temperature">
				description = <"xxxxxxxxxxx">
			>
			["at0.0.2"] = <
				text = <"Body exposure">
				description = <"xxxxxxxxxxx">
			>
			["at0.10.1"] = <
				text = <"data">
				description = <"xxxxxxxxxxx">
			>
			["at0.11.1"] = <
				text = <"state">
				description = <"xxxxxxxxxxx">
			>
			["at0.0.22"] = <
				text = <"Mouth">
				description = <"Temperature is measured within the mouth.">
			>
			["at0.0.23"] = <
				text = <"Ear canal">
				description = <"Temperature is measured from within the external auditory canal.">
			>
			["at0.0.24"] = <
				text = <"Axilla">
				description = <"Temperature is measured from the skin of the axilla with the arm positioned down by the side.">
			>
			["at0.0.25"] = <
				text = <"Rectum">
				description = <"Temperature measured within the rectum.">
			>
			["at0.0.26"] = <
				text = <"Nasopharynx">
				description = <"Temperature is measured within the nasopharynx.">
			>
			["at0.0.27"] = <
				text = <"Urinary bladder">
				description = <"Temperature is measured in the urinary bladder.">
			>
			["at0.0.28"] = <
				text = <"Intravascular">
				description = <"Temperature is measured within the vascular system.">
			>
			["at0.0.31"] = <
				text = <"Naked">
				description = <"No clothing, bedding or covering.">
			>
			["at0.0.32"] = <
				text = <"Reduced clothing/bedding">
				description = <"The person is covered by a lesser amount of clothing or bedding than deemed appropriate for the environmental circumstances.">
			>
			["at0.0.33"] = <
				text = <"Appropriate clothing/bedding">
				description = <"The person is covered by an amount of clothing or bedding deemed appropriate for the environmental circumstances.">
			>
			["at0.0.34"] = <
				text = <"Increased clothing/bedding">
				description = <"The person is covered by an increased amount of clothing or bedding than deemed appropriate for the environmental circumstances.">
			>
			["at0.0.43"] = <
				text = <"Skin">
				description = <"Temperature is measured from exposed skin.">
			>
			["at0.0.51"] = <
				text = <"Vagina">
				description = <"Temperature is measured within the vagina.">
			>
			["at0.0.54"] = <
				text = <"Oesophagus">
				description = <"Temperatue is measured within the oesophagus.">
			>
			["at0.0.55"] = <
				text = <"Inguinal skin crease">
				description = <"Temperature is measured in the inguinal skin crease between the leg and abdominal wall.">
			>
		>
	>
