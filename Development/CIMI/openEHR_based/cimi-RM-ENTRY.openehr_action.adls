archetype (adl_version=1.5)
	cimi-RM-ENTRY.openehr_action.v1

specialize
	cimi-COMPOSITION-ENTRY.openehr_care_entry.v1

language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"10/04/2012">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"EVALUATION class pattern from openEHR">
			keywords = <"observations", ...>
			copyright = <"© 2012 openEHR Foundation">
		>
	>
	lifecycle_state = <"Draft">
	other_details = <
		["model_level"] = <"reference">
	>

definition
	ENTRY[at0000.1] matches {	-- ACTION
		/data matches {
			ELEMENT[at0.2] occurrences matches {1} matches {	-- time
				value matches {
					DATE_TIME matches {*}
				}
			}
			ITEM[at0.3] occurrences matches {0..*} 	-- description 
			CLUSTER[at0.4] occurrences matches {0..*} matches {	-- ism_transition
				items matches {
					ELEMENT[at0.5] occurrences matches {1} matches {	-- current_state
						value matches {
							CODED_TEXT matches {*}
						}
					}
					ELEMENT[at0.6] occurrences matches {0..1} matches {	-- transition
						value matches {
							CODED_TEXT matches {*}
						}
					}
					ELEMENT[at0.7] occurrences matches {0..1} matches {	-- careflow_step
						value matches {
							CODED_TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			["at0000.1"] = <
				text = <"ACTION">
				description = <"ACTION class from openEHR">
			>
			["at0.2"] = <
				text = <"time">
				description = <"time property from ACTION class from openEHR">
			>
			["at0.3"] = <
				text = <"description ">
				description = <"description property from ACTION class from openEHR">
			>
			["at0.4"] = <
				text = <"ism_transition">
				description = <"ism_transition property from ACTION class from openEHR">
			>
			["at0.5"] = <
				text = <"current_state">
				description = <"current_state property from ISM_TRANSITION class from openEHR">
			>
			["at0.6"] = <
				text = <"transition">
				description = <"transition property from ISM_TRANSITION class from openEHR">
			>
			["at0.7"] = <
				text = <"careflow_step">
				description = <"careflow_step property from ISM_TRANSITION class from openEHR">
			>
		>
	>
