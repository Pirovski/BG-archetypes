archetype (adl_version=1.5.1)
	HL7-FHIR-Observation.blood_pressure2.v1.0.0

specialize
	HL7-FHIR-Observation.Observation.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale <thomas.beale@oceaninformatics.com>">
		["organisation"] = <"openEHR Foundation <http://www.openEHR.org>">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Version of BP that uses 'contained' to represent substructure inline">
			copyright = <"Copyright (c) 2012 openEHR Foundation ">
		>
	>
	lifecycle_state = <"unmanaged">

definition
	Observation[id1.1] matches {	-- Blood pressure
		method existence matches {0} 
		bodySite existence matches {0} 
		referenceRange existence matches {0} 
		/contained matches {
			use_archetype Observation[HL7-FHIR-Observation.blood_pressure_protocol2.v1] 
			use_archetype Observation[HL7-FHIR-Observation.blood_pressure_state2.v1] 
			use_archetype Observation[HL7-FHIR-Observation.blood_pressure_data2.v1] occurrences matches {1} 
		}
		value existence matches {0} 
		issued existence matches {0} 
		subject existence matches {1} 
		specimen existence matches {0} 
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Blood pressure">
				description = <"Blood pressure measurement">
			>
		>
	>
