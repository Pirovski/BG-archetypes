archetype (adl_version=1.5)
	openEHR-EHR-OBSERVATION.body_temp_test.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"18/04/2012">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"A parent archetype for testing various child refinements, including occurrences matches {0}. Content structure derived from openEHR Body Temperature archetype.">
			use = <"Test">
			keywords = <"test", "occurrences">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"Initial">
	other_details = <
		["regression"] = <"PASS">
	>

definition
	OBSERVATION[at0000] matches {	-- Body temperature
		data matches {
			HISTORY[at0002] matches {	-- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0003] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0001] matches {	-- Single
								items cardinality matches {1; unordered} matches {
									ELEMENT[at0004] matches {	-- Temperature
										value matches {
											DV_QUANTITY matches {
												property matches {[openehr::127]}
												[units, precision] matches {
													[{"°C"}, {1}],
													[{"°F"}, {1}]
												}
											}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0029] matches {	-- State
								items matches {
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- Body exposure
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0031, 	-- Naked
													at0032, 	-- Reduced clothing/bedding
													at0033, 	-- Appropriate clothing/bedding
													at0034; 	-- Increased clothing/bedding
													at0033]	-- assumed value
												}
											}
										}
									}
									allow_archetype CLUSTER[at0056] occurrences matches {0..1} matches {	-- Environmental Conditions
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.environmental_conditions\.v1/}
									}
									allow_archetype CLUSTER[at0057] occurrences matches {0..1} matches {	-- Exertion
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.level_of_exertion\.v1/}
									}
									allow_archetype ELEMENT[at0058] occurrences matches {0..1} matches {	-- Menstrual Cycle
										include
											archetype_id/value matches {/openEHR-EHR-ELEMENT\.last_normal_menstrual_period\.v1|openEHR-EHR-ELEMENT\.menstrual_cycle_day\.v1/}
									}
									ELEMENT[at0041] occurrences matches {0..1} matches {	-- Description of thermal stress
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0020] matches {	-- Protocol
				items matches {
					ELEMENT[at0021] occurrences matches {0..1} matches {	-- Site of measurement
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0022, 	-- Mouth
									at0023, 	-- Ear canal
									at0024, 	-- Axilla
									at0025, 	-- Rectum
									at0026, 	-- Nasopharynx
									at0027, 	-- Urinary bladder
									at0028, 	-- Intravascular
									at0043, 	-- Skin
									at0051, 	-- Vagina
									at0054, 	-- Oesophagus
									at0055]	-- Inguinal skin crease
								}
							}
						}
					}
					allow_archetype CLUSTER[at0059] occurrences matches {0..1} matches {	-- Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device\.v1/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			["at0000"] = <
				text = <"Body temperature">
				description = <"A measurement of the body temperature, which is a surrogate for the whole body temperature of the person.">
			>
			["at0003"] = <
				text = <"Any event">
				description = <"Any event">
			>
			["at0004"] = <
				text = <"Temperature">
				description = <"The measured body temperature (as a surrogate for the whole body).">
			>
			["at0021"] = <
				text = <"Site of measurement">
				description = <"The anatomical site of measurement of the temperature.">
			>
			["at0022"] = <
				text = <"Mouth">
				description = <"Temperature is measured within the mouth.">
			>
			["at0023"] = <
				text = <"Ear canal">
				description = <"Temperature is measured from within the external auditory canal.">
			>
			["at0024"] = <
				text = <"Axilla">
				description = <"Temperature is measured from the skin of the axilla with the arm positioned down by the side.">
			>
			["at0025"] = <
				text = <"Rectum">
				description = <"Temperature measured within the rectum.">
			>
			["at0026"] = <
				text = <"Nasopharynx">
				description = <"Temperature is measured within the nasopharynx.">
			>
			["at0027"] = <
				text = <"Urinary bladder">
				description = <"Temperature is measured in the urinary bladder.">
			>
			["at0028"] = <
				text = <"Intravascular">
				description = <"Temperature is measured within the vascular system.">
			>
			["at0029"] = <
				text = <"State">
				description = <"State information about the patient.">
			>
			["at0030"] = <
				text = <"Body exposure">
				description = <"The thermal situation of the person who is having the temperature taken.">
			>
			["at0031"] = <
				text = <"Naked">
				description = <"No clothing, bedding or covering.">
			>
			["at0032"] = <
				text = <"Reduced clothing/bedding">
				description = <"The person is covered by a lesser amount of clothing or bedding than deemed appropriate for the environmental circumstances.">
			>
			["at0033"] = <
				text = <"Appropriate clothing/bedding">
				description = <"The person is covered by an amount of clothing or bedding deemed appropriate for the environmental circumstances.">
			>
			["at0034"] = <
				text = <"Increased clothing/bedding">
				description = <"The person is covered by an increased amount of clothing or bedding than deemed appropriate for the environmental circumstances.">
			>
			["at0041"] = <
				text = <"Description of thermal stress">
				description = <"Description of the conditions applied to the subject that might influence their measured body temperature.">
			>
			["at0043"] = <
				text = <"Skin">
				description = <"Temperature is measured from exposed skin.">
			>
			["at0051"] = <
				text = <"Vagina">
				description = <"Temperature is measured within the vagina.">
			>
			["at0054"] = <
				text = <"Oesophagus">
				description = <"Temperatue is measured within the oesophagus.">
			>
			["at0055"] = <
				text = <"Inguinal skin crease">
				description = <"Temperature is measured in the inguinal skin crease between the leg and abdominal wall.">
			>
			["at0056"] = <
				text = <"Environmental Conditions">
				description = <"Details about the environmental conditions at the time of temperature measurement.">
			>
			["at0057"] = <
				text = <"Exertion">
				description = <"Details about the exertion of the person at the time of temperature measurement.">
			>
			["at0058"] = <
				text = <"Menstrual Cycle">
				description = <"Details about the menstrual cycle of a woman.">
			>
			["at0059"] = <
				text = <"Device">
				description = <"Details about the device use to measure body temperature.">
			>
		>
	>
	term_bindings = <
		["LNC205"] = <
			["/data[at0002]/events[at0003]/data[at0001]/items[at0004]"] = <[LNC205::8310-5]>
		>
	>
