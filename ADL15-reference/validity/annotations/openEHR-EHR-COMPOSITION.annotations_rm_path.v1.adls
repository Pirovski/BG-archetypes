archetype (adl_version=1.5)
	openEHR-EHR-COMPOSITION.annotations_rm_path.v1

language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"openEHR">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"9/11/2010">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Demonstrate annotations on RM paths (rather than archetype paths)">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_details = <
		["regression"] = <"PASS">
	>

definition
	COMPOSITION[at0000] matches {	-- Prescription
		category matches {
			DV_CODED_TEXT matches {
				defining_code matches {[openehr::433]}
			}
		}
		context matches {
			EVENT_CONTEXT matches {
				other_context matches {
					ITEM_TREE[at0001] matches {	-- Tree
						items matches {
							CLUSTER[at0002] occurrences matches {0..*} matches {	-- Qualification
								items matches {
									ELEMENT[at0003] occurrences matches {0..1} matches {	-- OrderID
										value matches {
											DV_EHR_URI matches {*}
											DV_IDENTIFIER matches {*}
										}
									}
									ELEMENT[at0004] occurrences matches {0..*} matches {	-- Endorsement
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0005] occurrences matches {0..*} matches {	-- AuthorisationID
										value matches {
											DV_IDENTIFIER matches {*}
										}
									}
									ELEMENT[at0006] occurrences matches {0..*} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		content cardinality matches {1..*; unordered} matches {
			allow_archetype INSTRUCTION[at0007] occurrences matches {1..*} matches {	-- Medication instruction
				include
					archetype_id/value matches {/openEHR-EHR-INSTRUCTION\.medication\.v1/}
			}
			allow_archetype ENTRY[at0008] occurrences matches {0..*} matches {	-- Other data
				include
					archetype_id/value matches {/.*/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			["at0000"] = <
				text = <"Prescription">
				description = <"A document authorising supply and administration of one or more medicines, vaccines or other therapeutic goods (as a collection of medication instrations) to be communicated to a dispensing or administration provider.">
			>
			["at0001"] = <
				text = <"Tree">
				description = <"@ internal @">
			>
			["at0002"] = <
				text = <"Qualification">
				description = <"Qualifications on any medication order included in the prescription.">
			>
			["at0003"] = <
				text = <"OrderID">
				description = <"The path or ID of the order referenced. If there is no OrderID then the endorsement relates to the entire prescription.">
			>
			["at0004"] = <
				text = <"Endorsement">
				description = <"Asserting that a special condition applies such as approval for  enhanced subsidy or concurrent supply. Australian examples include Regulation 24 (PBS), Hardship Conditions (RPBS) or CTG for 'close the gap'.">
			>
			["at0005"] = <
				text = <"AuthorisationID">
				description = <"An identifier authorising prescription, dispensing or reimbursement for this medication order.">
			>
			["at0006"] = <
				text = <"Comment">
				description = <"Comment on any qualification.">
			>
			["at0007"] = <
				text = <"Medication instruction">
				description = <"Contains one or more medication instructions to be supplied.">
			>
			["at0008"] = <
				text = <"Other data">
				description = <"Other observational or relevant data.">
			>
		>
	>

annotations
	items = <
		["en"] = <
			items = <
				["/context/start_time"] = <
					items = <
						["test"] = <"XXXX">
					>
				>
				["/context/location"] = <
					items = <
						["design note"] = <"Note on use of the non-archteyped context/location RM element in this data">
					>
				>
				["/context/health_care_facility/name"] = <
					items = <
						["design note"] = <"Note on use of non-archteyped context/health_care_facility/name RM element in this data">
					>
				>
			>
		>
	>

