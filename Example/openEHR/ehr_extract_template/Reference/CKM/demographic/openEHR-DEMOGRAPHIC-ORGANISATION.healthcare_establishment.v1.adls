archetype (adl_version=1.5)
	openEHR-DEMOGRAPHIC-ORGANISATION.healthcare_establishment.v1

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"01/04/2011">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Healthcare establishment demographic identifier">
			copyright = <"copyright (c) 2011 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>

definition
	ORGANISATION[at0000] matches {	
		identities cardinality matches {1..*; ordered} matches {
			PARTY_IDENTITY[at0001] occurrences matches {1..*} matches {	
				details matches {
					ITEM_LIST matches {	
						items cardinality matches {2..*; ordered} matches {
							ELEMENT[at0003] occurrences matches {1} matches {	
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0004] occurrences matches {1..*} matches {	
								value matches {
									DV_IDENTIFIER matches {
										issuer matches {*}
										id matches {*}
										type matches {*}
										realm matches {*}
									}
								}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Organisation">
					description = <"Organisation demographic data">
				>
				["at0001"] = <
					text = <"Identification">
					description = <"Identification - the names the organisation is known by">
				>
				["at0003"] = <
					text = <"Name">
					description = <"An organisation name">
				>
				["at0004"] = <
					text = <"Identifier">
					description = <"An organisation Identifier">
				>
			>
		>
	>
