archetype (adl_version=1.5)
	openEHR-EHR-EVALUATION.problem-diagnosis.v1
specialize
	openEHR-EHR-EVALUATION.problem.v1

language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"23/04/2006">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording medical diagnoses. Requires coded entry of diagnosis. ">
			use = <"Used for recording any diagnosis, present or past - so is used for recording past history as well as current diagnoses. Used with changed 'Subject of care' for recording diagnoses in relatives and so for family history.">
			keywords = <"past", "history", "family", "history", "condition">
			misuse = <"Use 'openEHR-EHR-EVALUATION.problem-diagnosis-histological' for histological diagnoses.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_details = <
		["MD5-CAM-1.0.1"] = <"CD3BA80C6AD89B3B8A8C7DC3125D9AF8">
		["references"] = <"">
	>

definition
	EVALUATION[at0000.1] matches {	-- Diagnosis
		/data[at0001]/items[at0002.1]/value matches {
			DV_CODED_TEXT matches {
				defining_code matches {[ac0.1]}		-- Any term that 'is_a' diagnosis
			}
		}
		/data[at0001]/items matches {
			before [at0003]
			ELEMENT[at0.32] occurrences matches {0..1} matches {	-- Status
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.33, 	-- provisional
							at0.34]	-- working
						}
					}
				}
			}
			after [at0031]
			CLUSTER[at0.35] occurrences matches {0..1} matches {	-- Diagnostic criteria
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0.36] occurrences matches {0..*} matches {	-- Criterion
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0.32"] = <
					text = <"Status">
					description = <"The status of the diagnosis">
				>
				["at0.33"] = <
					text = <"provisional">
					description = <"provisional diagnosis considered likely and a basis for proceeding with management">
				>
				["at0.34"] = <
					text = <"working">
					description = <"working diagnosis considered very likely but not yet confirmed">
				>
				["at0.35"] = <
					text = <"Diagnostic criteria">
					description = <"The criteria on which the diagnosis is based">
				>
				["at0.36"] = <
					text = <"Criterion">
					description = <"A basis for the diagnosis">
				>
				["at0000.1"] = <
					text = <"Diagnosis">
					description = <"A diagnosis defined by a clinician which is coded in an accepted terminology and may include the stage of the condition and the diagnostic criteria">
				>
				["at0002.1"] = <
					text = <"Diagnosis">
					description = <"The index diagnosis">
				>
			>
		>
	>
	constraint_definitions = <
		["en"] = <
			items = <
				["ac0.1"] = <
					text = <"Any term that 'is_a' diagnosis">
					description = <"Any term that is a diagnosis in an accepted terminology">
				>
			>
		>
	>
