archetype (adl_version=1.5.1; rm_release=1.0.2)
	openEHR-EHR_EXTRACT-EXTRACT.discharge_summary.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"Ocean Informatics">
		["date"] = <"01/04/2011">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Basic acute care establishment discharge summary">
			copyright = <"copyright (c) 2011 openEHR Foundation">
		>
	>
	lifecycle_state = <"unmanaged">

definition
	EXTRACT[id1] matches {	-- Simple discharge summary
		chapters matches {
			EXTRACT_CHAPTER[id3] matches {	-- Participations
				items matches {
					GENERIC_CONTENT_ITEM[id4] occurrences matches {1} matches {	-- Patient demographic content
						item matches {
							allow_archetype PERSON[id101] matches {	-- Patient demographics slot
								include
									archetype_id/value matches {/openEHR-DEMOGRAPHIC-PERSON\..*/}
								exclude
									archetype_id/value matches {/.*/}
							}
						}
					}
					GENERIC_CONTENT_ITEM[id5] occurrences matches {1} matches {	-- Healthcare establishment demographic content
						item matches {
							allow_archetype ORGANISATION[id102] matches {	-- Healthcare establishment demographics slot
								include
									archetype_id/value matches {/openEHR-DEMOGRAPHIC-ORGANISATION\..*/}
								exclude
									archetype_id/value matches {/.*/}
							}
						}
					}
					GENERIC_CONTENT_ITEM[id6] occurrences matches {1..*} matches {	-- Healthcare professional demographic content
						item matches {
							allow_archetype PERSON[id103] matches {	-- Healthcare individual provider demographics slot
								include
									archetype_id/value matches {/openEHR-DEMOGRAPHIC-PERSON\..*/}
								exclude
									archetype_id/value matches {/.*/}
							}
						}
					}
				}
			}
			EXTRACT_CHAPTER[id2] matches {	-- Subject
				items matches {
					GENERIC_CONTENT_ITEM[id7] occurrences matches {1} matches {	-- Clinical data
						item matches {
							allow_archetype COMPOSITION[id104] matches {	-- Patient discharge data slot
								include
									archetype_id/value matches {/openEHR-EHR-COMPOSITION\..*/}
								exclude
									archetype_id/value matches {/.*/}
							}
						}
					}
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Simple discharge summary">
				description = <"Discharge summary document for patient leaving a hospital">
			>
			["id2"] = <
				text = <"Subject">
				description = <"Chapter containing subject discharge information">
			>
			["id3"] = <
				text = <"Participations">
				description = <"Chapter containing participations information">
			>
			["id4"] = <
				text = <"Patient demographic content">
				description = <"Patient demographic content">
			>
			["id5"] = <
				text = <"Healthcare establishment demographic content">
				description = <"Healthcare establishment demographic content">
			>
			["id6"] = <
				text = <"Healthcare professional demographic content">
				description = <"Healthcare professional demographic content">
			>
			["id7"] = <
				text = <"Clinical data">
				description = <"Clinical discharge data for patient">
			>
			["id101"] = <
				text = <"Patient demographics slot">
				description = <"Patient demographics slot">
			>
			["id102"] = <
				text = <"Healthcare establishment demographics slot">
				description = <"Healthcare establishment demographics slot">
			>
			["id103"] = <
				text = <"Healthcare individual provider demographics slot">
				description = <"Healthcare individual provider demographics slot">
			>
			["id104"] = <
				text = <"Patient discharge data slot">
				description = <"Patient discharge data slot">
			>
		>
	>
