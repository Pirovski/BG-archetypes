archetype (adl_version=2.0.6; rm_release=1.0.0)
	openehr-ehr-DECISION_GROUP.determine_anthracyclines_chemo_regime.v0.0.1

language
	original_language = <[ISO_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Thomas Beale <thomas.beale@openehr.org>">
		["organisation"] = <"OpenEHR Foundation <http://www.openehr.org>">
	>
	custodian_namespace = <"org.openehr">
	custodian_organisation = <"openEHR Foundation">
	copyright = <"Copyright (c) 2018 openEHR Foundation">
	licence = <"Creative Commons CC-BY 4.0 unported <http://creativecommons.org/>">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Represent decision points 9 and 12 in the Moscow City breast cancer chemotherapy decision support map.">
		>
	>

definition
	DECISION_GROUP[id1] occurrences matches {1} matches {	-- Contraindications to anthracyclines
		override_type matches {1}
		members matches {
			DECISION_BRANCH[id24] occurrences matches {1} matches {	-- Contraindications - present
				members matches {
					PERFORMABLE_TASK<DEFINED_ACTION>[id26] occurrences matches {1} matches {	-- Recommend CMF chemo
						action matches {
							DEFINED_ACTION[id27] occurrences matches {1} matches {
								prototype matches {
									EVALUATION[id28] occurrences matches {1} 	-- Recommend CMF
								}
							}
						}
					}
				}
				value_constraint matches {
					CONTEXT_EXPRESSION<TYPE_DEF_BOOLEAN>[id82] matches {
						expression matches {"True"}
					}
				}
			}
			DECISION_BRANCH[id25] occurrences matches {1} matches {	-- Contraindications - not present
				members matches {
					DECISION_GROUP[id29] occurrences matches {1} matches {	-- Critical cardiopathology or age > 75
						override_type matches {1}
						members matches {
							DECISION_BRANCH[id30] occurrences matches {1} matches {	-- Present
								members matches {
									PERFORMABLE_TASK<DEFINED_ACTION>[id31] occurrences matches {1} matches {	-- Epirubicin + cycloposphamide
										action matches {
											DEFINED_ACTION[id32] occurrences matches {1} matches {
												prototype matches {
													EVALUATION[id33] occurrences matches {1} 	-- Recommend Epirubicin + cycloposphamide
												}
											}
										}
									}
								}
								value_constraint matches {
									CONTEXT_EXPRESSION<TYPE_DEF_BOOLEAN>[id84] matches {
										expression matches {"True"}
									}
								}
							}
							DECISION_BRANCH[id34] occurrences matches {1} matches {	-- Not present
								members matches {
									PERFORMABLE_TASK<DEFINED_ACTION>[id35] occurrences matches {1} matches {	-- AC
										action matches {
											DEFINED_ACTION[id36] occurrences matches {1} matches {
												prototype matches {
													EVALUATION[id37] occurrences matches {1} 	-- Recommend AC chemo
												}
											}
										}
									}
								}
								value_constraint matches {
									CONTEXT_EXPRESSION<TYPE_DEF_BOOLEAN>[id75] matches {
										expression matches {"False"}
									}
								}
							}
						}
						test matches {
							CONTEXT_EXPRESSION<TYPE_DEF_BOOLEAN>[id77] matches {
								expression matches {"$has_critical_cardiopathology or $age > 75"}
							}
						}
						review_dataset matches {
							DATASET_SPEC[id86] occurrences matches {0..1} 	-- Cardio-pathology data set
						}
						execution_type matches {1}
					}
				}
				value_constraint matches {
					CONTEXT_EXPRESSION<TYPE_DEF_BOOLEAN>[id79] matches {
						expression matches {"False"}
					}
				}
				execution_type matches {1}
			}
		}
		test matches {
			CONTEXT_EXPRESSION<TYPE_DEF_BOOLEAN>[id81] matches {
				expression matches {"$anthracyclines_contraindicated"}
			}
		}
		review_dataset matches {
			DATASET_SPEC[id85] occurrences matches {0..1} 	-- Review contra-indications anthracyclines data set
		}
		execution_type matches {1}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Contraindications to anthracyclines">
				description = <"-">
			>
			["id23"] = <
				text = <"Breast cancer chemo assess and prescribe">
				description = <"-">
			>
			["id24"] = <
				text = <"Contraindications - present">
				description = <"-">
			>
			["id26"] = <
				text = <"Recommend CMF chemo">
				description = <"-">
			>
			["id28"] = <
				text = <"Recommend CMF">
				description = <"-">
			>
			["id25"] = <
				text = <"Contraindications - not present">
				description = <"-">
			>
			["id29"] = <
				text = <"Critical cardiopathology or age > 75">
				description = <"-">
			>
			["id30"] = <
				text = <"Present">
				description = <"-">
			>
			["id31"] = <
				text = <"Epirubicin + cycloposphamide">
				description = <"-">
			>
			["id33"] = <
				text = <"Recommend Epirubicin + cycloposphamide">
				description = <"-">
			>
			["id34"] = <
				text = <"Not present">
				description = <"-">
			>
			["id35"] = <
				text = <"AC">
				description = <"-">
			>
			["id37"] = <
				text = <"Recommend AC chemo">
				description = <"-">
			>
			["id85"] = <
				text = <"Review contra-indications anthracyclines data set">
				description = <"-">
			>
			["id86"] = <
				text = <"Cardio-pathology data set">
				description = <"-">
			>
		>
	>
