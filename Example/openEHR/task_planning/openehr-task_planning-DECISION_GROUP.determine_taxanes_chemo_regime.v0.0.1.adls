archetype (adl_version=2.0.6; rm_release=1.1.0)
	openehr-task_planning-DECISION_GROUP.determine_taxanes_chemo_regime.v0.0.1

language
	original_language = <[ISO_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Thomas Beale <thomas.beale@openehr.org>">
		["organisation"] = <"OpenEHR Foundation <http://www.openehr.org>">
	>
	copyright = <"Copyright (c) 2016 openEHR Foundation">
	licence = <"Creative Commons CC-BY 4.0 unported <http://creativecommons.org/>">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"???">
		>
	>

definition
	DECISION_GROUP[id1] matches {	-- Determine taxanes regime
		test matches {
			BOOLEAN_CONTEXT_EXPRESSION[id2] matches {
				expression matches {"$taxanes_contraindicated"}
			}
		}
		members matches {
			DECISION_BRANCH[id4] occurrences matches {1} matches {	-- Taxanes contraindicated
				value_constraint matches {
					BOOLEAN_CONTEXT_EXPRESSION[id6] matches {
						expression matches {"True"}
					}
				}
				members matches {
					use_archetype DECISION_GROUP[id34, openehr-task_planning-DECISION_GROUP.determine_anthracyclines_chemo_regime.v0.0.1] occurrences matches {1} 
				}
			}
			DECISION_BRANCH[id5] occurrences matches {1} matches {	-- Taxanes not contraindicated
				value_constraint matches {
					BOOLEAN_CONTEXT_EXPRESSION[id8] matches {
						expression matches {"False"}
					}
				}
				members matches {
					DECISION_GROUP[id10] occurrences matches {1} matches {	-- Anthracyclines contraindicated?
						test matches {
							BOOLEAN_CONTEXT_EXPRESSION[id11] matches {
								expression matches {"$anthracyclines_contraindicated"}
							}
						}
						members matches {
							DECISION_BRANCH[id13] occurrences matches {1} matches {	-- Not contraindicated
								value_constraint matches {
									BOOLEAN_CONTEXT_EXPRESSION[id17] matches {
										expression matches {"False"}
									}
								}
								members matches {
									use_archetype TASK_GROUP[id22, openehr-task_planning-ADHOC_GROUP.choose_ac_chemo_regime.v0.0.1] occurrences matches {1} 
									use_archetype DECISION_GROUP[id23, openehr-task_planning-DECISION_GROUP.check_brca_mutation.v0.0.1] occurrences matches {1} 
								}
							}
							DECISION_BRANCH[id14] occurrences matches {1} matches {	-- Contraindicated
								value_constraint matches {
									BOOLEAN_CONTEXT_EXPRESSION[id15] matches {
										expression matches {"True"}
									}
								}
								members matches {
									TASK[id19] occurrences matches {1} matches {	-- Recommend DC therapy
										action matches {
											DEFINED_ACTION[id20] occurrences matches {1} matches {
												prototype matches {
													EVALUATION[id21] occurrences matches {1} 	-- Recommend DC chemotherapy
												}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		review_dataset matches {
			REVIEW_DATASET_SPEC[id32] occurrences matches {1} 	-- Taxanes contraindications dataset
		}
		override_type matches {
			OVERRIDE_TYPE[id33] occurrences matches {1} 
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Determine taxanes regime (BPM node 10)">
				description = <"-">
			>
			["id4"] = <
				text = <"Taxanes contraindicated">
				description = <"-">
			>
			["id5"] = <
				text = <"Taxanes not contraindicated">
				description = <"-">
			>
			["id10"] = <
				text = <"Anthracyclines contraindicated? (BPM node 14)">
				description = <"-">
			>
			["id13"] = <
				text = <"Not contraindicated">
				description = <"-">
			>
			["id14"] = <
				text = <"Contraindicated">
				description = <"-">
			>
			["id19"] = <
				text = <"Recommend DC therapy">
				description = <"-">
			>
			["id21"] = <
				text = <"Recommend DC chemotherapy">
				description = <"-">
			>
			["id22"] = <
				text = <"Choose AC chemo">
				description = <"-">
			>
			["id23"] = <
				text = <"Consider BRCA mutation">
				description = <"-">
			>
			["id32"] = <
				text = <"Taxanes contraindications dataset">
				description = <"-">
			>
			["id34"] = <
				text = <"Non-taxane regime">
				description = <"-">
			>
		>
	>
